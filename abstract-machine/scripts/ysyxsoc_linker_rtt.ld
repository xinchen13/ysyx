ENTRY(_start)
PHDRS {fsbl PT_LOAD; ssbl PT_LOAD; text PT_LOAD; data_ext PT_LOAD; rodata PT_LOAD; data PT_LOAD; bss_ext PT_LOAD; bss PT_LOAD;}

MEMORY {
    mrom  : ORIGIN = 0x20000000, LENGTH = 4K
    sram  : ORIGIN = 0x0f000000, LENGTH = 8K
    psram : ORIGIN = 0x80000000, LENGTH = 4M
    flash : ORIGIN = 0x30000000, LENGTH = 16M
    sdram : ORIGIN = 0xa0000000, LENGTH = 32M
}

SECTIONS {
    . = ORIGIN(flash);

    /* first stage bootloader */
    .fsbl : {
        . = ALIGN(0x4);
        *(entry)
        *(.fsbl)
        . = ALIGN(0x4);
    } > flash AT> flash : fsbl

    /* second stage bootloader */
    _ssbl_section_src = LOADADDR(.ssbl);
    .ssbl : {
        _ssbl_section_start = .;
        . = ALIGN(0x4);
        *(.ssbl)
        _ssbl_section_end = .;
        . = ALIGN(0x4);
    } > sram AT> flash : ssbl

    /* text section */
    _text_section_src = LOADADDR(.text);
    .text : {
        . = ALIGN(0x4);
        _text_section_start = .;
        *(.text*)
        . = ALIGN(0x4);
        _text_section_end = .;
    } > sdram AT> flash : text
    etext = .;
    _etext = .;

    .data.extra : {
        /* section information for finsh shell */
        __fsymtab_start = .;
        KEEP(*(FSymTab))
        __fsymtab_end = .;
        . = ALIGN(8);
        __vsymtab_start = .;
        KEEP(*(VSymTab))
        __vsymtab_end = .;
        . = ALIGN(8);

        /* section information for initial. */
        . = ALIGN(8);
        __rt_init_start = .;
        KEEP(*(SORT(.rti_fn*)))
        __rt_init_end = .;
        . = ALIGN(8);

        __rt_utest_tc_tab_start = .;
        KEEP(*(UtestTcTab))
        __rt_utest_tc_tab_end = .;

        . = ALIGN(8);
        __am_apps_data_start = .;
        *(__am_apps.data*)
        *(__am_apps.sdata*)
        __am_apps_data_end = .;
        . = ALIGN(8);
    } > sdram AT> flash : data_ext

    /* read-only data section */
    .rodata : {
        . = ALIGN(0x4);
        *(.rodata*)
        *(.srodata*)
        . = ALIGN(0x4);
    } > sdram AT> flash : rodata

    /* data section */
    _data_section_src = LOADADDR(.data);
    .data : {
        . = ALIGN(0x4);
        _data_section_start = .;
        *(.data*)
        *(.sdata*)
        . = ALIGN(0x4);
        _data_section_end = .;
    } > sdram AT> flash : data 
    edata = .;
    _data = .;

    .bss.extra : {
        _bss_start = .;
        . = ALIGN(8);
        __am_apps_bss_start = .;
        *(__am_apps.bss*)
        *(__am_apps.sbss*)
        *(__am_apps.scommon*)
        __am_apps_bss_end = .;
        . = ALIGN(8);
    } > sdram AT> flash : bss_ext

    /* bss section */
    .bss : {
        . = ALIGN(0x4);
        *(.bss*)
        *(.sbss*)
        *(.scommon)
        . = ALIGN(0x4);
        _bss_end = .;
    } > sdram AT> flash : bss

    _heap_start = ORIGIN(psram);
    _heap_end = ORIGIN(psram) + LENGTH(psram);

    _stack_top = ORIGIN(sram);
    _stack_pointer = _stack_top + LENGTH(sram);

    end = .;
    _end = .;
}
